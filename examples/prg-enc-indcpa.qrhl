# Formalization of one of the examples in "Quantum Relational Hoare Logic" (https://arxiv.org/abs/1802.03188)

isabelle PrgEnc.

ambient var rho : program_state.

classical var k : key.
classical var s : key.

# Variables for adversary communication
classical var m1 : msg.
classical var m2 : msg.
classical var c : msg.
classical var r : msg.
classical var b : bit.

quantum var qglobA : string.
classical var cglobA : string.

# A1: inputs: none; outputs m1,m2
adversary A1 free m1,m2,cglobA,qglobA.
# A: inputs: c; outputs: b
adversary A2 free c,b,cglobA,qglobA.

# B1/B2: inputs: r; outputs: b
program B1 := { call A1; c <- r+m1; call A2; }.
program B2 := { call A1; c <- r+m2; call A2; }.

program indcpa0 := {
  k <$ uniform UNIV;
  call A1;
  c <- enc(k,m1);
  call A2;
}.

program indcpa1 := {
  k <$ uniform UNIV;
  call A1;
  c <- enc(k,m2);
  call A2;
}.

program prg0B1 := {
  s <$ uniform UNIV;
  r <- G(s);
  call B1;
}.

program prg1B1 := {
  r <$ uniform UNIV;
  call B1;
}.

program prg0B2 := {
  s <$ uniform UNIV;
  r <- G(s);
  call B2;
}.

program prg1B2 := {
  r <$ uniform UNIV;
  call B2;
}.

lemma indcpa0_prg0B1: Pr[b=1:indcpa0(rho)] = Pr[b=1:prg0B1(rho)].

 byqrhl.
 simp.
 inline indcpa0.
 inline prg0B1.
 inline B1.
 equal.
  simp!.
 wp left.
 wp right.
 simp.
 equal.
  simp!.
 simp.
 wp right.
 rnd.
 skip.
 simp!.
qed.

lemma prg1B1_prg1B21: Pr[b=1:prg1B1(rho)] = Pr[b=1:prg1B2(rho)].

 byqrhl.
 simp.
 inline prg1B1.
 inline prg1B2.
 inline B1.
 inline B2.
 equal.
  simp!.
 wp left.
 wp right.
 swap left 1 1.
 swap right 1 1.
 rnd r,r <- map_distr (Î»r. (r,r+m11+m22)) (uniform UNIV).
 simp aux_bij.
 equal.
  simp!.
 simp.
 skip.
 simp!.
qed.

lemma indcpa1_prg0B2: Pr[b=1:indcpa1(rho)] = Pr[b=1:prg0B2(rho)].

 byqrhl.
 simp.
 inline indcpa1.
 inline prg0B2.
 inline B2.
 equal.
  simp!.
 wp left.
 wp right.
 simp.
 equal.
  simp!.
 simp.
 wp right.
 rnd.
 skip.
 simp!.
qed.


lemma final: abs( Pr[b=1:indcpa0(rho)] - Pr[b=1:indcpa1(rho)] ) <=
             abs( Pr[b=1:prg0B1(rho)] - Pr[b=1:prg1B1(rho)] ) +
             abs( Pr[b=1:prg0B2(rho)] - Pr[b=1:prg1B2(rho)] ).

 simp ! indcpa0_prg0B1 indcpa1_prg0B2 prg1B1_prg1B21.
qed.
