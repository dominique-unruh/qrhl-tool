isabelle /opt/Isabelle2016-1, theory PrgEnc.

ambient var rho : program_state.

classical var k : key.
classical var s : key.

# Variables for adversary communication
classical var m1 : msg.
classical var m2 : msg.
classical var c : msg.
classical var r : msg.
classical var b : bit.

quantum var qglobA : string.
classical var cglobA : string.

# A: inputs: none; outputs m1,m2
adversary A1 vars m1,m2,cglobA,qglobA.
# A: inputs: c; outputs: b
adversary A2 vars c,b,cglobA,qglobA.

# B: inputs: r; outputs: b
program B1 := { call A1; c <- r+m1; call A2; }.
program B2 := { call A1; c <- r+m2; call A2; }.


program indcpa0 := {
  k <$ uniform UNIV;
  call A1;
  c <- enc(k,m1);
  call A2;
}.

program indcpa1 := {
  k <$ uniform UNIV;
  call A1;
  c <- enc(k,m2);
  call A2;
}.

program prg0B1 := {
  s <$ uniform UNIV;
  r <- G(s);
  call B1;
}.

program prg1B1 := {
  r <$ uniform UNIV;
  call B1;
}.

program prg0B2 := {
  s <$ uniform UNIV;
  r <- G(s);
  call B2;
}.

program prg1B2 := {
  r <$ uniform UNIV;
  call B2;
}.

program middle := {
  call A1;
  c <$ uniform UNIV;
  call A2;
}.

goal indcpa0_prg0B1: Pr[b:indcpa0(rho)] = Pr[b:prg0B1(rho)].

 byqrhl.
 simp.
 inline indcpa0.
 inline prg0B1.
 inline B1.
 call.
 wp left.
 wp right.
 simp.
 call.
 simp.
 wp right.
 rnd.
 skip.
 simp.
 true.

qed.

goal prg1B1_prg1B21: Pr[b:prg1B1(rho)] = Pr[b:prg1B2(rho)].

 byqrhl.
 simp.
 inline prg1B1.
 inline prg1B2.
 inline B1.
 inline B2.
 call.
 wp left.
 wp right.
 swap left.
 swap right.
 rnd r,r <- map_distr (Î»r. (r,r+m11+m22)) (uniform UNIV).
 simp aux_bij mysimp2.
 call.
 simp.
 skip.
 simp.
 true.

qed.

goal indcpa1_prg0B2: Pr[b:indcpa1(rho)] = Pr[b:prg0B2(rho)].

 byqrhl.
 simp.
 inline indcpa1.
 inline prg0B2.
 inline B2.
 call.
 wp left.
 wp right.
 simp.
 call.
 simp.
 wp right.
 rnd.
 skip.
 simp.
 true.
    
qed.


goal abs( Pr[b:indcpa0(rho)] - Pr[b:indcpa1(rho)] ) <=
     abs( Pr[b:prg0B1(rho)] - Pr[b:prg1B1(rho)] ) +
     abs( Pr[b:prg0B2(rho)] - Pr[b:prg1B2(rho)] ).

 simp indcpa0_prg0B1 indcpa1_prg0B2 prg1B1_prg1B21.
 simp triangle.
 true.
qed.
